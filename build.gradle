import groovy.json.JsonSlurper

apply plugin: 'java'

sourceCompatibility = 14
targetCompatibility = 1.8

sourceSets.main{
    java.srcDirs = ['src']
    resources.srcDirs = ['assets']
}

repositories{
    mavenCentral()
    maven{url 'https://www.jitpack.io'}
}

ext{
    def json = new JsonSlurper()
    def modFile = file("${sourceSets.main.resources.srcDirs.last()}/mod.json")
    def mod = json.parseText(modFile.text)

    artifactBaseName = mod.name + (!mod.version.isEmpty() ? "-$mod.version" : "")

    versions = [:]
    versions.mindustry = '128'
    versions.jabel = '0.4.1'
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$versions.mindustry"
    compileOnly "com.github.Anuken.Mindustry:core:$versions.mindustry"

    annotationProcessor "com.github.MindustryInside:jabel:$versions.jabel"
}

task dist(type: Jar){
    archiveBaseName.set artifactBaseName

    from{
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }

    with jar
}

tasks.withType(JavaCompile){
    if(JavaVersion.current() != JavaVersion.VERSION_1_8){
        options.compilerArgs = ['--release', '8', '--enable-preview']
    }

    options.compilerArgs << '-Xplugin:jabel'

    doFirst{
        options.compilerArgs = options.compilerArgs.findAll{it != '--enable-preview'}
    }
}
